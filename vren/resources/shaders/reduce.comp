#version 460

#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_arithmetic : require

#extension GL_EXT_debug_printf : enable

#ifndef VREN_WORKGROUP_SIZE
#	define VREN_WORKGROUP_SIZE 1024
#endif

#define VREN_MAX_ITEMS 1

#ifndef VREN_DATA_TYPE
#	error "Required definition not specified `VREN_DATA_TYPE`"
#endif

#ifndef VREN_OPERATION
#	error "Required definition not specified `VREN_OPERATION`"
#endif

#define UINT32_MAX 0xffffffffu

layout(local_size_x = VREN_WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(push_constant) uniform PushConstants
{
	uint num_items;
	uint base_level;
	uint block_length;
};

layout(set = 0, binding = 0) buffer InputBuffer
{
	VREN_DATA_TYPE input_buffer[];
};

layout(set = 0, binding = 1) buffer OutputBuffer // Could be the InputBuffer itself
{
	VREN_DATA_TYPE output_buffer[];
};

shared VREN_DATA_TYPE s_data[1024 * VREN_MAX_ITEMS];

void main()
{
	uint block_offset = gl_WorkGroupID.y * block_length;
	
    uint offset = (1u << base_level) - 1;
    uint stride = 1u << base_level;

	// Global memory read
	for (uint i = 0; i < num_items; i++)
	{
		uint data_idx = offset + (gl_GlobalInvocationID.x * num_items + i) * stride;
		s_data[gl_LocalInvocationID.x * num_items + i] = data_idx < block_length ? input_buffer[block_offset + data_idx] : VREN_DATA_TYPE(0);
	}

	// Invocation-wide reduction
	for (uint level = 0; level < log2(num_items); level++)
	{
        for (uint i = 0; i < (num_items >> (level + 1)); i++)
        {
            uint a_idx = (gl_LocalInvocationID.x * num_items) + (1 << level) - 1 + (i << (level + 1));
            uint b_idx = a_idx + (1 << level);

            s_data[b_idx] = VREN_OPERATION(s_data[a_idx], s_data[b_idx]);
        }
	}
	
	barrier();

	// Workgroup-wide reduction
	uint thread_value_idx = (gl_LocalInvocationID.x + 1) * num_items - 1;

	for (uint level = 0; level < uint(log2(VREN_WORKGROUP_SIZE)); level++)
	{
		uint level_mask = (1 << (level + 1)) - 1; 
		if ((gl_LocalInvocationID.x & level_mask) == level_mask)
		{
			uint b_idx = thread_value_idx;
			uint a_idx = b_idx - (1 << level) * num_items;

			s_data[b_idx] = VREN_OPERATION(s_data[b_idx], s_data[a_idx]);
		}
		
		barrier();
	}

	// Global memory write
	for (uint i = 0; i < num_items; i++)
	{
		uint data_idx = offset + (gl_GlobalInvocationID.x * num_items + i) * stride;
		if (data_idx < block_length)
		{
			output_buffer[block_offset + data_idx] = s_data[gl_LocalInvocationID.x * num_items + i];
		}
	}
}
