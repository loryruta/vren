#version 460

#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_KHR_shader_subgroup_shuffle : require

#extension GL_EXT_debug_printf : enable

#define UINT32_MAX 0xffffffffu

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

#define NUM_ITERS 16

layout(push_constant) uniform PushConstants
{
	uint offset;
	uint stride;
	uint step_levels;
};

layout(set = 0, binding = 0) buffer Buffer
{
	uint data[]; // Length must be a power of 2
};

void main()
{
	uint thread_values[NUM_ITERS];

	for (uint i = 0; i < NUM_ITERS; i++)
	{
		uint data_idx = offset + (gl_GlobalInvocationID.x * NUM_ITERS + i) * stride;
		thread_values[i] = data_idx < data.length() ? data[data_idx] : 0;
	}
	
	uint remaining_levels = step_levels;

	uint thread_value = thread_values[NUM_ITERS - 1];

	for (int level = int(log2(32)) - 1; level >= 0 && remaining_levels > 0; level--, remaining_levels--)
	{
		uint thread_idx = gl_SubgroupInvocationID.x;

		uint level_mask = (1 << level) - 1; 
		uint active_threads = subgroupBallot((thread_idx & level_mask) == level_mask).x;

		uint sum_mask = (level + 1) < 5 ? (1 << (1 << (level + 1))) - 1 : UINT32_MAX;

		uint shift_by = 1 << (level + 1);
		for (; sum_mask != 0; sum_mask = shift_by < 32 ? sum_mask << shift_by : 0)
		{
			uint op_threads = active_threads & sum_mask;

			if (subgroupInverseBallot(uvec4(op_threads)))
			{
				uint sum = subgroupAdd(thread_value);

				uint right_thread_mask = 1 << findMSB(op_threads);
				uint right_value = subgroupShuffle(thread_value, right_thread_mask);

				if (subgroupInverseBallot(uvec4(1 << findLSB(op_threads))))
				{
					thread_value = right_value;
				}

				if (subgroupInverseBallot(uvec4(right_thread_mask)))
				{
					thread_value = sum;
				}
			}
		}
	}

	thread_values[NUM_ITERS - 1] = thread_value;

	for (int level = int(log2(NUM_ITERS)) - 1; level >= 0 && remaining_levels > 0; level--, remaining_levels--)
	{
        for (uint item_idx = 0; item_idx < (NUM_ITERS >> (level + 1)); item_idx++)
        {
            uint a_idx = (1 << level) - 1 + (item_idx << (level + 1));
            uint b_idx = (1 << level) - 1 + (item_idx << (level + 1)) + (1 << level);

			uint tmp = thread_values[b_idx];
            thread_values[b_idx] = thread_values[a_idx] + tmp;
			thread_values[a_idx] = tmp;
        }
	}

	for (uint i = 0; i < NUM_ITERS; i++)
	{
		uint data_idx = offset + (gl_GlobalInvocationID.x * NUM_ITERS + i) * stride;
		if (data_idx < data.length())
		{
			data[data_idx] = thread_values[i];
		}
	}
}
